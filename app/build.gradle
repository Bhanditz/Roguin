apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

android {
    def properties = new Properties()
    properties.load(rootProject.file("local.properties").newDataInputStream())

    compileSdkVersion Version.Sdk.BASE

    compileOptions {
        sourceCompatibility Version.Core.JAVA
        targetCompatibility Version.Core.JAVA
    }

    defaultConfig {
        applicationId "com.izikode.izilib.roguindemo"

        minSdkVersion Version.Sdk.MIN
        targetSdkVersion Version.Sdk.BASE

        versionCode Version.Build.CODE
        versionName "${ Version.Build.MAJOR }.${ Version.Build.MINOR }"

        manifestPlaceholders = [
                facebook_api_key   : properties.getProperty('facebook_key', ''),
                twitter_api_key    : properties.getProperty('twitter_key', ''),
                twitter_api_secret : properties.getProperty('twitter_secret', '')
        ]
    }

    signingConfigs {
        debug {
            storeFile     file("jks/roguinDemo.jks")
            storePassword properties.getProperty('demo_keystore_password', '')
            keyAlias      properties.getProperty('demo_keystore_alias', '')
            keyPassword   properties.getProperty('demo_keystore_password', '')
        }
    }

    buildTypes {
        debug {
            minifyEnabled false
        }
    }

    configurations.all {
        resolutionStrategy.eachDependency { details ->
            if (details.requested.group == 'com.android.support' && !details.requested.name.contains('multidex') ) {
                details.useVersion Version.Sdk.COMPAT
            }
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:${Version.Core.KOTLIN}"

    implementation "com.android.support:appcompat-v7:${ Version.Sdk.COMPAT }"
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    
    implementation project(':roguin')
}
